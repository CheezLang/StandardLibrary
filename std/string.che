io   :: import std.io
util :: import std.util
C    :: import std.c
fmt  :: import std.fmt

use import std.array
use import std.hash
use import std.unicode
use import std.printable
use import std.mem.allocator
use import std.mem.std_heap_allocator
import std.alloca

if const @is_os("windows") {
    use import std.os.windows
    use import std.os.windows_functions
    use import std.os.windows_constants
}

#export_scope

/// UTF-8 encoded string data type for dynamically resizable strings and automatic resource management.
/// Supports custom allocators or no allocators at all (e.g. the string can use memory on the stack).
String :: struct {
    data      : []mut u8
    length    : int
    allocator : ^Allocator
}

impl Drop for String {
    drop :: (&Self) {
        if allocator != null {
            free_n(data, allocator)
        }
        // data = null
        // length = 0
    }
}

impl String {
    /// Creates an empty string with the provided __allocator__ and a capacity of 0
    empty :: (allocator: ^Allocator = ^DEFAULT_STD_HEAP_ALLOCATOR) -> Self {
        return String(null, 0, allocator)
    }

    /// Creates an empty string with the provided __capacity__ and __allocator__
    with_capacity :: (cap: int, allocator: ^Allocator = ^DEFAULT_STD_HEAP_ALLOCATOR) -> Self {
        result := String(null, 0, allocator)
        result.reserve(cap)
        return result
    }

    /// Creates a string from the provided pointer __ptr__ and capacity __cap__. The string has no allocator so it can not grow over capacity and will not free the memory.
    from_raw_ptr :: (ptr: ^mut u8, cap: int) -> Self {
        mut sl : []mut u8 = ptr
        sl.length = cap
        return String(sl, 0, null)
    }

    /// Creates a string from the provided string __str__ and __allocator__
    from_string :: (str: string, allocator: ^Allocator = ^DEFAULT_STD_HEAP_ALLOCATOR) -> Self {
        s := Self.empty(allocator)
        s.append_string(str)
        return s
    }

    /// Creates a string from the provided c-string __str__ and allocator
    from_cstring :: (str: c_string, allocator: ^Allocator = ^DEFAULT_STD_HEAP_ALLOCATOR) -> Self {
        s := Self.empty(allocator)
        s.append_cstring(str)
        return s
    }

    /// Tries to create a string from the provided UTF-16 bytes __str__.
    /// Only implemented on windows so far.
    from_utf16 :: (str: []u16) -> Result[String, ()] {
        if const @is_os("windows") {
            required_size := WideCharToMultiByte(CP_UTF8, 0, str.data, cast str.length, null, 0, null, null)
            result := String.empty()
            result.resize(required_size)
            bytes_written := WideCharToMultiByte(CP_UTF8, 0, str.data, cast str.length, result.get_raw(), required_size, null, null)
            if bytes_written == 0 {
                return Err(())
            }
            return Ok(result)
        } else {
            @assert(false)
            return Err()
        }
    }

    /// Returns the capacity of the string.
    get_capacity :: (&Self) -> int {
        return data.length
    }

    /// Returns the length of the string in bytes.
    get_length :: (&Self) -> int {
        return length
    }

    /// Returns a pointer to the underlying byte array.
    get_raw :: (&Self) -> ^u8 {
        return data.data
    }

    /// Returns the ASCII character at byte offset __i__.
    char_at :: (&Self, i: int) -> char #operator("[]") {
        code_point, len := Utf8.decode(data[i..data.length])
        return code_point
    }

    /// Sets the ASCII character at index __i__ to __c__
    /// 
    ///     s.set_char_at(5, 'H')
    ///     s[5] = 'H'
    set_char_at :: (&mut Self, i: int, c: u8) #operator("set[]") {
        @assert(i >= 0 and i < length)
        *data[i] = c
    }

    /// Makes sure that there is enough capacity for _self.length_ + __amount__ ASCII characters.
    reserve_more :: (&mut Self, amount: int) {
        reserve(length + amount)
    }

    /// Makes sure that there is enough capacity for __size__ ASCII characters.
    reserve :: (&mut Self, size: int) {
        if data.length > size {
            return
        }

        data.length = size
        if data.data == null {
            length = 0
            data = alloc_n(u8, cast size, allocator)
        } else {
            data = realloc_n(data, cast size, allocator)
            if length > data.length {
                length = data.length
            }
        }
    }

    /// Clears the string. The capacity remains unchanged.
    clear :: (&mut Self) {
        length = 0
    }

    /// Sets the length in bytes of the string to __size__
    resize :: (&mut Self, size: int) {
        self.reserve(size)
        length = size
    }

    /// Appends the String __other__ to this string.
    append :: (&mut Self, other: &Self) -> &Self #operator("+=") {
        if other.data.data == null {
            return self
        }
        newLength := length + other.length
        self.reserve(newLength)
        C.memcpy(cast ^data.data[length], cast other.data.data, cast(uint) (other.length * @sizeof(u8)))
        length = newLength
        return self
    }

    /// Appends the string __other__ to this string.
    append_string :: (&mut Self, other: string) -> &Self #operator("+=") {
        newLength := length + other.bytes.length
        self.reserve(newLength)
        C.memcpy(cast ^data.data[length], cast other.bytes.data, cast other.bytes.length)
        length = newLength
        return self
    }

    /// Appends the c-string __other__ to this string.
    append_cstring :: (&mut Self, other: c_string) -> &Self #operator("+=") {
        otherLen := C.strlen(other)
        newLength := length + int(otherLen)
        self.reserve(newLength)
        C.memcpy(cast ^data.data[length], cast other, cast otherLen)
        length = newLength
        return self
    }

    /// Appends the unicode codepoint __ch__ to this string.
    append_char :: (&mut Self, ch: char) -> &Self #operator("+=") {
        bytes, count := Utf8.encode(ch)
        newLength := length + int(count)
        self.reserve(newLength)
        C.memcpy(^*data[length], ^*bytes[0], cast count)
        length = newLength
        return self
    }

    /// Returns a slice of this string starting at __from__ up to __to__ (exclusive). (__from__..__to__)
    sliceFT :: (&Self, from: int, to: int) -> string {
        slice := string(^*data[from], to - from)
        return slice
    }

    /// Returns a slice of this string starting at __from__ and with length __length__. (__from__..(__from__+__length__))
    sliceFL :: (&Self, from: int, len: int) -> string {
        slice := string(^*data[from], len)
        return slice
    }

    /// Returns a slice of the entire string. (..)
    slice :: (&Self) -> string {
        return self.sliceFL(0, length)
    }

    /// Returns a slice of the string starting at __from__ to the end. (__from__..)
    sliceF :: (&Self, from: int) -> string {
        return self.sliceFL(from, length - from)
    }
    
    /// Returns a slice of the string from the beginning with length __len__. (0..__len__)
    sliceL :: (&Self, len: int) -> string {
        return self.sliceFL(0, len)
    }

    /// Returns the first index of ASCII character __ch__ or -1 if not found. Note: this doesn't work with Unicode yet!
    index_of :: (&Self, ch: char) -> int {
        for i in 0..length {
            if *data[i] == u8(ch) {
                return i
            }
        }

        return -1
    }

    /// Returns a c-string pointing to the content of this string. You should not modify the string while you use the c-string.
    as_cstring :: (&mut Self) -> c_string {
        reserve(length + 1)
        *data[length] = 0u8
        return cast data.data
    }
}

impl Equatable[String] for String {
    /// Returns true if the two strings are equal (same bytes).
    equal :: (&Self, other: &String) -> bool {
        str := self.slice()
        oth := other.slice()
        return streq_string(str, oth)
    }
}

impl Equatable[string] for String {
    /// Returns true if the two strings are equal (same bytes).
    equal :: (&Self, other: &string) -> bool {
        str := self.slice()
        return streq_string(str, *other)
    }
}

impl Equatable[string] for string {
    /// Returns true if the two strings are equal (same bytes).
    equal :: (&Self, b: &string) -> bool {
        return streq_string(*self, *b)
    }
}

impl Hashable for String {
    /// Returns a hash of the string.
    hash :: (&Self) -> uint {
        str := self.slice()
        return str.hash()
    }
}

impl Clone for String {
    /// Returns a copy of the string.
    clone :: (&Self) -> Self {
        other := String.empty()
        other.append(self)
        return other
    }
}

impl String {
    append_print :: (&mut Self, pr: ^Printable) -> &Self #operator("+=") {
        pr.print(self, "")
        return self
    }

    appendf :: (&mut Self, format: string, arguments: $T) {
        if const @is_tuple(T) {
            if const T.length == 0 {
                args : [1]^Printable = [^arguments]
            } else {
                args : [T.length]^Printable = default
                @for_tuple_values(arguments, (v, i) => {
                    if const @types_match(@typeof(v), &$_) {
                        *args[i] = ^*v
                    } else {
                        *args[i] = ^v
                    }
                })
            }
        } else {
            args : [1]^Printable = [cast(^T) ^arguments]
        }
        self.append_printables(format, args)
    }


    append_printables :: (&mut Self, format: string, args: []^Printable) {
        mut formatSpecifier := {
            len :: 64
            mem := @alloca(u8, len)
            String.from_raw_ptr(mem.data, mem.length)
        }

        //len      := format.bytes.length
        mut state    := 0
        mut argIndex := 0

        // state    - desc
        // 0        - normal
        // 1        - in format
        // 2        - escape

        for c in format {
            if state == 0 {
                if c == '``' {
                    state = 2
                } else if c == '{' {
                    state = 1
                } else {
                    self += c
                }
            } else if state == 2 {
                self += c
                state = 0
            } else if state == 1 {
                if c == '}' {
                    state = 0

                    mut format := ""

                    colon := formatSpecifier.index_of(':')
                    if colon >= 1 {
                        // colon -> arg index before colon
                        argIndex = fmt.parse_int(formatSpecifier.sliceL(colon))
                    } else if colon == -1 and formatSpecifier.length > 0 {
                        // no colon but non empty -> arg index
                        argIndex = fmt.parse_int(formatSpecifier.slice())
                    }
                    if colon != -1 {
                        // colon -> format after colon
                        format = formatSpecifier.sliceF(colon + 1)
                    }

                    if argIndex >= args.length {
                        C.printf("[%lld] `"%.*s`": arg index to out of bounds: %lld, args: %lld`n", it_index, format.bytes.length, format.bytes.data, argIndex, args.length)
                        return
                    }

                    (*args[argIndex]).print(self, format)
                    argIndex += 1
                    formatSpecifier.length = 0

                } else {
                    &formatSpecifier += c
                }
            }
        }
    }

    format :: (format: string, args: $T) -> Self {
        s := Self.empty()
        s.appendf(format, args)
        return s
    }

    /// Returs an array of slices of the string split at the ASCII character __c__. If __remove_empty__ is __true__ then empty entries are not included.
    split_at_char :: (&Self, c: char, remove_empty: bool = false) -> Array[string] {
        result := Array[string].create()

        mut start := 0
        for i in 0..length {
            if *data[i] == u8(c) {
                sub := self.sliceFT(start, i)
                if sub.bytes.length > 0 or !remove_empty {
                    result.add(sub)
                }

                start = i + 1
            }
        }

        if start < length {
            sub := self.sliceF(start)
            if sub.bytes.length > 0 or !remove_empty {
                result.add(sub)
            }
        }

        return result
    }
}

StringLineIterator :: struct {
    remaining: string
}

for_extension_string_line_iterator :: (self: StringLineIterator, body: Code) #for {
    it_index := 0
    bytes := self.remaining.bytes

    loop {
        if bytes.length <= 0 then break
        defer {it_index += 1}

        line_end  := -1
        for c, i in bytes {
            if char(c) == '`n' {
                line_end = i
                break
            }
        }

        // if no newline was found, then the line is the remainder of the string
        if line_end == -1 {
            line_end = bytes.length
        }

        line    := bytes[0 .. line_end]
        bytes   = bytes[(line_end + 1) .. bytes.length]
        it      := string(line.data, line.length)
        @insert(body)
    }
}

StringSplitCharIterator :: struct {
    remaining   : string
    split_at    : char
    remove_empty: bool
}

impl StringSplitCharIterator {
    to_array :: (Self) -> Array[string] {
        result := Array[string].create()
        for part in self {
            result.add(part)
        }
        return result
    }
}

for_extension_string_split_char_iterator :: (self: StringSplitCharIterator, body: Code) #for {
    mut it_index := 0

    while self.remaining.bytes.length > 0 {
        index := self.remaining.index_of(self.split_at)
        it := if index < 0 {
            self.remaining <- ""
        } else {
            tmp := self.remaining[..index]
            self.remaining = self.remaining[(index+1)..]
            tmp
        }

        if it.bytes.length != 0 {
            @insert(body)
            it_index += 1
        }
    }
}

impl Printable for String {
    print :: (&Self, str: &mut String, format: string) {
        str += self
    }
}

streql :: (a: $T, b: $T2, len: int) -> bool {
    alen := T.get_length(a)
    blen := T2.get_length(b)
    if len > alen {
        len = alen
    }
    if len > blen {
        len = blen
    }

    for i in 0..len {
        if T.char_at(a, i) != T2.char_at(b, i) {
            return false
        }
    }
    return true
}

streq :: (a: $T, b: $T2) -> bool {
    len  := T.get_length(a)
    blen := T2.get_length(b)

    if len != blen {
        return false
    }

    for i in 0..len {
        if T.char_at(a, i) != T2.char_at(b, i) {
            return false
        }
    }
    return true
}

streq_string :: (a: string, b: string) -> bool {
    return streq(a, b)
}

impl string {
    get_length :: (Self) -> int {
        // @todo: calculate length in chars
        return self.bytes.length
    }
    
    char_at :: (Self, i: int) -> char {
        return char(*self[i])
    }

    to_string :: (Self) -> String {
        return String.from_string(self)
    }

    to_owned :: (Self) -> String {
        return String.from_string(self)
    }

    slice :: (Self, from: int = 0, mut length: int = -1) -> string {
        if length < 0 {
            length = self.bytes.length - from
        }

        result := string(util.pointer_add_sized(self.bytes.data, from), length)
        return result
    }

    lines :: (Self) -> StringLineIterator {
        return StringLineIterator(self)
    }

    /// Returns an iterator which iterates over substrings separated by the ASCII character __c__. If __remove_empty__ is __true__ then empty entries are not included.
    split_at_char :: (Self, c: char, remove_empty: bool = false) -> StringSplitCharIterator {
        return StringSplitCharIterator(self, c, remove_empty)
    }

    /// Returns whether the string starts with __prefix__
    starts_with :: (Self, prefix: string) -> bool {
        self_bytes := self.bytes
        pref_bytes := prefix.bytes
        if pref_bytes.length > self_bytes.length {
            return false
        }

        for i in 0 .. pref_bytes.length {
            if *self_bytes[i] != *pref_bytes[i] {
                return false
            }
        }

        return true
    }

    /// Returns whether the string ends with __prefix__
    ends_with :: (Self, postfix: string) -> bool {
        self_bytes := self.bytes
        post_bytes := postfix.bytes
        if post_bytes.length > self_bytes.length {
            return false
        }

        offset := self_bytes.length - post_bytes.length

        for i in 0 .. post_bytes.length {
            if *self_bytes[offset + i] != *post_bytes[i] {
                return false
            }
        }

        return true
    }

    sub_string :: (Self, from_byte: int, mut to_byte: int = -1) -> string {
        if to_byte == -1 {
            to_byte = self.bytes.length
        }
        @assert(
            from_byte >= 0 and
            from_byte <= self.bytes.length and
            from_byte <= to_byte and
            to_byte <= self.bytes.length)
        sub_bytes := self.bytes[from_byte .. to_byte]
        return string(sub_bytes.data, sub_bytes.length)
    }

    ///* Returns a slice starting at the first non-whitespace character and ending at the last non-whitespace charactor.
    ### Examples
        "   hello string".trim() == "hello string"
        "hello string   ".trim() == "hello string"
        "   hello string   ".trim() == "hello string"
    */
    trim :: (Self) -> string {
        if self.bytes.length == 0 {
            return ""
        }

        mut start := -1
        mut end   := -1

        for self.bytes {
            c := char(it)
            if !char.is_whitespace(c) {
                if start == -1 {
                    start = it_index
                }

                end = it_index
            }
        }

        if start == -1 and end == -1 {
            // only whitespace in string
            return ""
        }

        end += 1
        @assert(start >= 0 and start < self.bytes.length and start <= end and end <= self.bytes.length)
        return self.sub_string(start, end)
    }

    contains :: (Self, ch: char) -> bool {
        for c in self {
            if c == ch {
                return true
            }
        }

        return false
    }

    count_char :: (Self, ch: char) -> int {
        mut count := 0
        for c in self {
            if c == ch {
                count += 1
            }
        }

        return count
    }

    index_of :: (Self, ch: char) -> int {
        // @todo: properly handle utf8
        bytes := self.bytes
        for i in 0..bytes.length {
            if *bytes[i] == u8(ch) {
                return i
            }
        }

        return -1
    }

    last_index_of :: (Self, ch: char) -> int {
        ch_bytes, ch_len := Utf8.encode(ch)

        for i in self.bytes.length - int(ch_len) .. 0 #label outer {
            for k in 0 .. int(ch_len) {
                if *self.bytes[i + k] != *ch_bytes[k] then continue outer
            }
            return i
        }

        return -1
    }

    index_of_string :: (Self, str: string) -> int {
        if str.bytes.length == 0 {
            return 0
        }

        bytes := self.bytes
        for i in 0..self.bytes.length #label outer {
            for k in 0..str.bytes.length {
                if *self[i + k] != *str[k] then continue outer
            }

            return i
        }

        return -1
    }

    to_cstring :: (Self) -> c_string {
        mut str_c := alloc_raw(char8, cast self.bytes.length + 1)
        str_c[self.bytes.length] = cast 0
        C.memcpy(cast str_c, cast self.bytes.data, cast self.bytes.length)
        return str_c
    }

    to_utf16 :: (Self) -> Result[Array[u16], ()] {
        if const @is_os("windows") {
            required_size := MultiByteToWideChar(CP_UTF8, 0, self.bytes.data, cast self.bytes.length, null, 0)
            wide_char_str := Array[u16].create(cast required_size + 1)
            wide_char_str.resize(required_size)
            *wide_char_str.access_unchecked_mut(required_size) = 0
            bytes_written := MultiByteToWideChar(CP_UTF8, 0, self.bytes.data, cast self.bytes.length, wide_char_str.get_raw(), required_size)
            if bytes_written < required_size {
                @assert(false)
                return Err(())
            }
            return Ok(wide_char_str)
        } else {
            @assert(false)
            return Err()
        }
    }

    from_cstring :: (str: ^char8) -> string {
        return string(cast str, cast C.strlen(str))
    }
}

impl Into[String] for string {
    into :: (&Self) -> String {
        return self.to_owned()
    }
}

impl char {
    /// Returns true if the character is a space (`' '`), tab (`'\t'`), carriage return (`'\r'`) or line ending (`'\n'`)
    is_whitespace :: (Self) -> bool {
        return match self {
            ' ' -> true
            '`t' -> true
            '`r' -> true
            '`n' -> true
            _ -> false
        }
    }

    /// Returns true if the character matches the regex __[a-zA-Z]__
    is_alpha :: (Self) -> bool {
        return (self >= 'a'  and self <= 'z') or (self >= 'A' and self <= 'Z')
    }

    /// Returns true if the character matches the regex __[0-9]__
    is_digit :: (Self) -> bool {
        return (self >= '0'  and self <= '9')
    }

    /// Returns true if the character matches the regex __[0-1]__
    is_bin_digit :: (Self) -> bool {
        return (self >= '0'  and self <= '1')
    }

    /// Returns true if the character matches the regex __[0-9a-FA-F]__
    is_hex_digit :: (Self) -> bool {
        return (self >= '0'  and self <= '9') or (self >= 'a'  and self <= 'f') or (self >= 'A' and self <= 'F')
    }
}

impl c_string {
    get_length :: (Self) -> int {
        return cast C.strlen(self)
    }
    
    char_at :: (Self, i: int) -> char {
        return char(self[i])
    }

    to_string :: (Self) -> String {
        return String.from_cstring(self)
    }
}

for_extension_string_builtin :: (arr: string, code: Code) #for {
    bytes := arr.bytes

    while mut it_index := 0, it_index < bytes.length {
        it, len := Utf8.decode(bytes[it_index..bytes.length])
        @insert(code, _break=break, _continue=continue)
        it_index += int(len)
    }
}